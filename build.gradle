/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'zackbeach' at '2/17/17 7:16 PM' with Gradle 3.1
 */


plugins {
  // Shadow
  id "com.github.johnrengelman.shadow" version "5.2.0"
  // Launch4J
  id "edu.sc.seis.launch4j" version "2.4.8"
  // Spotless
  id "com.diffplug.spotless" version "5.6.1"
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'application'
apply plugin: 'eclipse'

repositories {
  jcenter()
  mavenCentral()
  maven { url "https://jitpack.io" }
}

// In this section you declare the dependencies for your production and test code
dependencies {
  // Apache POI 4.1.2
  compile 'org.apache.poi:poi:4.1.2'
  compile 'org.apache.poi:poi-scratchpad:4.1.2'
  compile 'org.apache.poi:poi-ooxml-schemas:4.1.2'
  compile 'org.apache.poi:poi-ooxml:4.1.2'
  compile 'org.apache.poi:poi-excelant:4.1.2'
  compile 'org.apache.poi:poi-examples:4.1.2'
  compile 'org.apache.poi:ooxml-security:1.1'
  compile 'org.apache.poi:ooxml-schemas:1.4'
  compile 'org.apache.poi:poi-contrib:3.6'

  // Apache Commons CSV 1.8
  compile 'org.apache.commons:commons-csv:1.8'

  // Git repositories
  // ZB Utils
  compile 'com.github.zbeach:zb-utils:v1.1'
  // Swing Extensions
  compile 'com.github.zbeach:swing-extensions:v1.1'
}

mainClassName = 'main.RunsheetGenerator'

// Spotless
spotless {
  java {
    googleJavaFormat()
  }
}

// Standard JAR build
jar {
  manifest {
    attributes(
      'Main-Class' : 'main.RunsheetGenerator'
    )
  }
}

// Shadow JAR distribution
shadowJar {
  baseName = 'RunsheetGenerator'
  classifier = null
  version = null
  manifest {
    attributes(
      'Main-Class' : 'main.RunsheetGenerator'
    )
  }
}

// Launch4J EXE distribution
launch4j {
  mainClassName = 'main.RunsheetGenerator'
  outfile = 'Runsheet Generator.exe'
  outputDir = 'distributions/launch4j'
  icon = 'res/icon.ico'
}

// Javadoc
task makeJavadocs(type: Javadoc) {
  source = sourceSets.main.allJava
  classpath = configurations.compile
}

configurations {
  jaxDoclet
}
